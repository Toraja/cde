set shell := ['/usr/bin/bash', '-c']

export PATH := env_var('HOME') / '.local/bin:' + env_var('PATH')
export ANSIBLE_BECOME_PASSWORD_FILE := '.ansible_become_password'

playbook_check_flags := '--check --diff'

default:
	@just --list --unsorted

[private]
@echored args:
	echo -e "\e[0;31m{{args}}\e[0;0m"

[private]
playbook playbook *flags:
	ansible-playbook {{flags}} playbooks/{{playbook}}.yml

[private]
playbook-check playbook:
	just playbook {{playbook}} {{playbook_check_flags}}

github_repo_subpath := 'personal.github.com/Toraja'
github_host_user := 'personal.github.com:Toraja'
toybox_repo_url := 'git@' + github_host_user / 'toybox.git'
cde_repo_url := 'git@' + github_host_user / 'cde.git'

# Clone necessary repositories, setup gitconfig and create symlinks to the repositories
repositories:
	#!/usr/bin/env bash
	if [[ ! -d ~/toybox ]]; then
		git clone {{toybox_repo_url}} ~/toybox
	fi
	if ! diff --brief ./gitconfig ~/.gitconfig; then
		cat ./gitconfig > ~/.gitconfig
	fi
	ghq_root=$(ghq root)
	if [[ -d ~/toybox && ! -L ~/toybox ]]; then
		mkdir --parents ${ghq_root}/{{github_repo_subpath}} && \
			mv ~/toybox ${ghq_root}/{{github_repo_subpath}} && \
			ln --symbolic ${ghq_root}/{{github_repo_subpath}}/toybox ~/toybox
	fi
	if [[ -d ~/cde && ! -L ~/cde ]]; then
		mv ~/cde ${ghq_root}/{{github_repo_subpath}} && \
			ln --symbolic ${ghq_root}/{{github_repo_subpath}}/cde ~/cde
	fi

fish:
	just playbook fish

fish-check:
	just playbook-check fish

vim:
	ansible-playbook playbooks/vim.yml

tmux:
	sudo --preserve-env apt-get update
	sudo --preserve-env apt-get install --no-install-recommends --yes tmux
	git clone --depth 1 https://github.com/tmux-plugins/tpm ~/.config/tmux/plugins/tpm
	mkdir --parents ~/.config/tmux/ && echo 'source ~/toybox/tmux/tmux.conf' > ~/.config/tmux/tmux.conf
	~/.config/tmux/plugins/tpm/bin/install_plugins

docker: docker-clean docker-add-repository docker-install-engine docker-setup

docker-clean:
	sudo --preserve-env apt-get remove docker docker.io containerd runc
	@# docker-engine might not exist as package
	-sudo --preserve-env apt-get remove docker-engine

docker-add-repository:
	sudo --preserve-env apt-get update
	sudo --preserve-env apt-get install --no-install-recommends --yes apt-transport-https ca-certificates curl gnupg lsb-release
	curl --fail --silent --show-error --location https://download.docker.com/linux/ubuntu/gpg | sudo --preserve-env gpg --dearmor -o /usr/share/keyrings/docker-archive-keyring.gpg
	@# check CPU architecure and exit if it does not match
	@if [ $(uname --machine) != 'x86_64' ]; then just echored the CPU architecure is not supported; exit 1; fi
	echo "deb [arch=amd64 signed-by=/usr/share/keyrings/docker-archive-keyring.gpg] https://download.docker.com/linux/ubuntu \
		$(lsb_release -cs) stable" | sudo --preserve-env tee /etc/apt/sources.list.d/docker.list > /dev/null

docker-install-engine: # prerequisites commented out for reusability -- docker-clean docker-add-repository
	sudo --preserve-env apt-get update
	sudo --preserve-env apt-get install --no-install-recommends --yes docker-ce docker-ce-cli containerd.io docker-compose-plugin
	./github-latest-release-installer.sh -x docker buildx 'buildx-v.*.linux-amd64'  ~/.docker/cli-plugins/docker-buildx

docker-setup:
	sudo --preserve-env usermod -aG docker $(whoami)
	mkdir --parents ~/.docker && cat ~/toybox/docker/config.json > ~/.docker/config.json

# Run this recipe to setup on WSL
docker-wsl:
	@# This is needed for Ubuntu 22.04
	@# Source:
	@# - https://dev.to/felipecrs/simply-run-docker-on-wsl2-3o8
	@# - https://medium.com/twodigits/install-docker-on-wsl-2-with-vpn-support-to-replace-docker-for-windows-45b8e200e171
	sudo update-alternatives --set iptables /usr/sbin/iptables-legacy
	sudo update-alternatives --set ip6tables /usr/sbin/ip6tables-legacy

taskwarrior:
	ansible-playbook playbooks/taskwarrior.yml

misc:
	ansible-playbook playbooks/misc.yml

[group('Test')]
test:
	@# UID is a readonly variable, so it cannot be reassigned as in UID=${UID}
	export UID && docker compose run --rm test

[group('Test')]
build-testenv image-tag='':
	@# COMPOSE_BAKE=true delegates build to docker bake
	export UID && COMPOSE_BAKE=true IMAGE_TAG={{image-tag}} docker compose build test

[group('Test')]
renew-test-base:
	docker pull ubuntu:latest
