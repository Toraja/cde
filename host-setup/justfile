set shell := ['/usr/bin/bash', '-c']

mod playbooks

export PATH := env_var('HOME') / '.local/bin:' + env_var('PATH')

default:
	@just --list --unsorted

github_repo_subpath := 'personal.github.com/Toraja'
github_host_user := 'personal.github.com:Toraja'
toybox_repo_url := 'git@' + github_host_user / 'toybox.git'
cde_repo_url := 'git@' + github_host_user / 'cde.git'

# Clone necessary repositories, setup gitconfig and create symlinks to the repositories
repositories:
	#!/usr/bin/env bash
	if [[ ! -d ~/toybox ]]; then
		git clone {{toybox_repo_url}} ~/toybox
	fi
	if ! diff --brief ./fixtures/gitconfig ~/.gitconfig; then
		cat ./fixtures/gitconfig > ~/.gitconfig
	fi
	ghq_root=$(ghq root)
	if [[ -d ~/toybox && ! -L ~/toybox ]]; then
		mkdir --parents ${ghq_root}/{{github_repo_subpath}} && \
			mv ~/toybox ${ghq_root}/{{github_repo_subpath}} && \
			ln --symbolic ${ghq_root}/{{github_repo_subpath}}/toybox ~/toybox
	fi
	if [[ -d ~/cde && ! -L ~/cde ]]; then
		mv ~/cde ${ghq_root}/{{github_repo_subpath}} && \
			ln --symbolic ${ghq_root}/{{github_repo_subpath}}/cde ~/cde
	fi

# Fails to start taskwarrior/tasknote inside containers without these directories which are pointed by symlinks in containers.
taskwarrior-dirs:
	mkdir --parents ~/hosthome/.local/share/taskwarrior/task ~/hosthome/.local/share/taskopen/notes

[group('Test')]
test:
	@# UID is a readonly variable, so it cannot be reassigned as in UID=${UID}
	export UID && docker compose run --rm test

[group('Test')]
build-testenv image-tag='':
	@# COMPOSE_BAKE=true delegates build to docker bake
	export UID && COMPOSE_BAKE=true IMAGE_TAG={{image-tag}} docker compose build test

[group('Test')]
renew-test-base:
	docker pull ubuntu:latest
