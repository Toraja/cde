set shell := ['/usr/bin/bash', '-c']

export PATH := env_var('HOME') / '.local/bin:' + env_var('PATH')
export ANSIBLE_BECOME_PASSWORD_FILE := '.ansible_become_password'

playbook_check_flags := '--check --diff'

default:
	@just --list --unsorted

[private]
@echored +args:
	echo -e "\e[0;31m{{args}}\e[0;0m"

[private]
@echocyan +args:
	echo -e "\e[0;36m{{args}}\e[0;0m"

[private]
playbook tags *flags:
	ansible-playbook --tags={{tags}} {{flags}} playbooks/main.yml

[private]
playbook-check tags:
	just playbook {{tags}} {{playbook_check_flags}}

github_repo_subpath := 'personal.github.com/Toraja'
github_host_user := 'personal.github.com:Toraja'
toybox_repo_url := 'git@' + github_host_user / 'toybox.git'
cde_repo_url := 'git@' + github_host_user / 'cde.git'

# Clone necessary repositories, setup gitconfig and create symlinks to the repositories
repositories:
	#!/usr/bin/env bash
	if [[ ! -d ~/toybox ]]; then
		git clone {{toybox_repo_url}} ~/toybox
	fi
	if ! diff --brief ./fixtures/gitconfig ~/.gitconfig; then
		cat ./fixtures/gitconfig > ~/.gitconfig
	fi
	ghq_root=$(ghq root)
	if [[ -d ~/toybox && ! -L ~/toybox ]]; then
		mkdir --parents ${ghq_root}/{{github_repo_subpath}} && \
			mv ~/toybox ${ghq_root}/{{github_repo_subpath}} && \
			ln --symbolic ${ghq_root}/{{github_repo_subpath}}/toybox ~/toybox
	fi
	if [[ -d ~/cde && ! -L ~/cde ]]; then
		mv ~/cde ${ghq_root}/{{github_repo_subpath}} && \
			ln --symbolic ${ghq_root}/{{github_repo_subpath}}/cde ~/cde
	fi

galaxy-install:
	ansible-galaxy role install --role-file requirements.yml --roles-path playbooks/roles/

docker:
	just playbook docker

docker-check:
	just playbook-check docker

docker-wsl:
	just playbook docker,wsl
	echocyan Setting to enable systemd might be added to /etc/wsl.conf. In that case, you need to restart WSL.

docker-wsl-check:
	just playbook-check docker,wsl

fish:
	just playbook fish

fish-check:
	just playbook-check fish

neovim:
	just playbook neovim

neovim-check:
	just playbook-check neovim

taskwarrior:
	just playbook taskwarrior

taskwarrior-check:
	just playbook-check taskwarrior

tmux:
	just playbook tmux

tmux-check:
	just playbook-check tmux

misc:
	just playbook misc

misc-check:
	just playbook-check misc

[group('Test')]
test:
	@# UID is a readonly variable, so it cannot be reassigned as in UID=${UID}
	export UID && docker compose run --rm test

[group('Test')]
build-testenv image-tag='':
	@# COMPOSE_BAKE=true delegates build to docker bake
	export UID && COMPOSE_BAKE=true IMAGE_TAG={{image-tag}} docker compose build test

[group('Test')]
renew-test-base:
	docker pull ubuntu:latest
